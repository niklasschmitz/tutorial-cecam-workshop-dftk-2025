{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Elastic constants"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We compute *clamped-ion* elastic constants of a crystal using\n",
    "the algorithmic differentiation density-functional perturbation theory (AD-DFPT) approach\n",
    "as introduced in [^SPH25].\n",
    "\n",
    "[^SPH25]:\n",
    "    Schmitz, N. F., Ploumhans, B., & Herbst, M. F. (2025)\n",
    "    *Algorithmic differentiation for plane-wave DFT: materials design, error control and learning model parameters.*\n",
    "    [arXiv:2509.07785](https://arxiv.org/abs/2509.07785)\n",
    "\n",
    "We consider a crystal in its equilibrium configuration, where all atomic forces\n",
    "and stresses vanish.  Homogeneous strains $η$ are then applied\n",
    "relative to this relaxed structure.\n",
    "The elastic constants are derived from the stress-strain relationship.\n",
    "In [Voigt notation](https://en.wikipedia.org/wiki/Voigt_notation),\n",
    "the stress $\\sigma$ and strain $\\eta$ tensors are represented as 6-component vectors.\n",
    "The elastic constants $C$ are then given by\n",
    "the Jacobian of the stress with respect to strain, forming a $6 \\times 6$ matrix\n",
    "$$\n",
    "  C = \\frac{\\partial \\sigma}{\\partial \\eta}.\n",
    "$$\n",
    "\n",
    "The sparsity pattern of the matrix $C$ follows from crystal symmetry\n",
    "and is tabulated in standard references (eg. Table 9 in [^Nye1985]).\n",
    "This sparsity can be used a priori to reduce the number of strain patterns\n",
    "that need to be probed to extract all independent components of $C$.\n",
    "For example, cubic crystals have only three independent elastic constants\n",
    "$C_{11}$, $C_{12}$ and $C_{44}$, with the pattern\n",
    "$$\n",
    "C = \\begin{pmatrix}\n",
    "  C_{11} & C_{12} & C_{12} & 0      & 0      & 0 \\\\\n",
    "  C_{12} & C_{11} & C_{12} & 0      & 0      & 0 \\\\\n",
    "  C_{12} & C_{12} & C_{11} & 0      & 0      & 0 \\\\\n",
    "  0      & 0      & 0      & C_{44} & 0      & 0 \\\\\n",
    "  0      & 0      & 0      & 0      & C_{44} & 0 \\\\\n",
    "  0      & 0      & 0      & 0      & 0      & C_{44} \\\\\n",
    "\\end{pmatrix}.\n",
    "$$\n",
    "Thus we can just choose a suitable strain pattern $\\dot{\\eta} = (1, 0, 0, 1, 0, 0)^\\top$,\n",
    "such that $C\\dot{\\eta} = (C_{11}, C_{12}, C_{12}, C_{44}, 0, 0)^\\top$. That is,\n",
    "for cubic crystals like diamond silicon we obtain all independent elastic\n",
    "constants from a single Jacobian-vector product on the stress-strain function.\n",
    "\n",
    "[^Nye1985]:\n",
    "     Nye, J. F. (1985).\n",
    "     *Physical Properties of Crystals*. Oxford University Press.\n",
    "     Comment: Since the elastic tensor transforms equivariantly under rotations,\n",
    "     its numerical components depend on the chosen Cartesian coordinate frame.\n",
    "     These tabulated patterns assume a standardized orientation of the structure\n",
    "     with respect to conventional crystallographic axes.\n",
    "\n",
    "This example computes the *clamped-ion* elastic tensor, keeping internal\n",
    "atomic positions fixed under strain.  The *relaxed-ion* tensor includes\n",
    "additional corrections from internal relaxations, which can be obtained\n",
    "from first-order atomic displacements in DFPT (see [^Wu2005]).\n",
    "\n",
    "[^Wu2005]:\n",
    "    Wu, X., Vanderbilt, D., & Hamann, D. R. (2005).\n",
    "    *Systematic treatment of displacements, strains, and electric fields in density-functional perturbation theory.*\n",
    "    [Physical Review B, 72(3), 035105](https://doi.org/10.1103/PhysRevB.72.035105)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32m\u001b[1m  Activating\u001b[22m\u001b[39m project at `~/git/tutorial-cecam-workshop-dftk-2025`\n"
     ]
    }
   ],
   "source": [
    "import Pkg\n",
    "Pkg.activate(joinpath(@__DIR__, \"..\"))\n",
    "\n",
    "using DFTK\n",
    "using PseudoPotentialData\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using DifferentiationInterface\n",
    "using AtomsBuilder\n",
    "using Unitful\n",
    "using UnitfulAtomic"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "pseudopotentials = PseudoFamily(\"dojo.nc.sr.pbe.v0_4_1.standard.upf\")\n",
    "a0_pbe = 10.33u\"bohr\"  # Equilibrium lattice constant of silicon with PBE\n",
    "model0 = model_DFT(bulk(:Si; a=a0_pbe); pseudopotentials, functionals=PBE())\n",
    "\n",
    "Ecut = recommended_cutoff(model0).Ecut\n",
    "kgrid = [4, 4, 4]\n",
    "tol = 1e-6\n",
    "\n",
    "function symmetries_from_strain(model0, voigt_strain)\n",
    "    lattice = DFTK.voigt_strain_to_full(voigt_strain) * model0.lattice\n",
    "    model = Model(model0; lattice, symmetries=true)\n",
    "    model.symmetries\n",
    "end\n",
    "\n",
    "strain_pattern = [1., 0., 0., 1., 0., 0.];  # recovers [c11, c12, c12, c44, 0, 0]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "For elastic constants beyond the bulk modulus, symmetry-breaking strains\n",
    "are required. That is, the symmetry group of the crystal is reduced.\n",
    "Here we simply precompute the relevant subgroup by applying the automatic\n",
    "symmetry detection (spglib) to the finitely perturbed crystal."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -8.453520355075                   -0.94    5.3    5.08s\n",
      "  2   -8.455630775648       -2.68       -1.77    1.0    2.27s\n",
      "  3   -8.455776321229       -3.84       -2.88    1.9    640ms\n",
      "  4   -8.455790066922       -4.86       -3.27    3.0    240ms\n",
      "  5   -8.455790174436       -6.97       -3.78    1.2    153ms\n",
      "  6   -8.455790186833       -7.91       -4.92    1.4    141ms\n",
      "  7   -8.455790187706       -9.06       -5.52    2.9    206ms\n",
      "  8   -8.455790187713      -11.18       -5.97    1.6    149ms\n",
      "  9   -8.455790187713      -12.37       -6.51    1.3    151ms\n",
      "Solving response problem\n",
      "Iter  Restart  Krydim  log10(res)  avg(CG)  Δtime   Comment\n",
      "----  -------  ------  ----------  -------  ------  ---------------\n",
      "                                      66.1   4.23s  Non-interacting\n",
      "   1        0       1        0.11     50.0   8.05s  \n",
      "   2        0       2       -0.70     45.9   1.16s  \n",
      "   3        0       3       -1.71     39.6   541ms  \n",
      "   4        0       4       -2.78     32.3   460ms  \n",
      "   5        0       5       -4.04     22.8   362ms  \n",
      "   6        0       6       -5.56      8.8   739ms  \n",
      "   7        0       7       -7.80      4.0   170ms  \n",
      "   8        1       1       -6.61     65.6   963ms  Restart\n",
      "                                      65.1   893ms  Final orbitals\n"
     ]
    }
   ],
   "source": [
    "symmetries_strain = symmetries_from_strain(model0, 0.01 * strain_pattern)\n",
    "\n",
    "\n",
    "function stress_from_strain(model0, voigt_strain; symmetries, Ecut, kgrid, tol)\n",
    "    lattice = DFTK.voigt_strain_to_full(voigt_strain) * model0.lattice\n",
    "    model = Model(model0; lattice, symmetries)\n",
    "    basis = PlaneWaveBasis(model; Ecut, kgrid)\n",
    "    scfres = self_consistent_field(basis; tol)\n",
    "    DFTK.full_stress_to_voigt(compute_stresses_cart(scfres))\n",
    "end\n",
    "\n",
    "stress_fn(voigt_strain) = stress_from_strain(model0, voigt_strain;\n",
    "                                             symmetries=symmetries_strain,\n",
    "                                             Ecut, kgrid, tol)\n",
    "stress, (dstress,) = value_and_pushforward(stress_fn, AutoForwardDiff(),\n",
    "                                           zeros(6), (strain_pattern,));"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We can inspect the stress to verify it is small (close to equilibrium):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element StaticArraysCore.SVector{6, Float64} with indices SOneTo(6):\n",
       " -2.3121110372192445e-5\n",
       " -2.31211337804883e-5\n",
       " -2.3121133780488326e-5\n",
       " -1.1930420092768635e-10\n",
       "  0.0\n",
       "  0.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "stress"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The response of the stress to `strain_pattern` contains the elastic constants\n",
    "in atomic units, with the expected pattern $(C_{11}, C_{12}, C_{12}, C_{44}, 0, 0)$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element StaticArraysCore.SVector{6, Float64} with indices SOneTo(6):\n",
       " 0.0053199437119925705\n",
       " 0.0020248998061615977\n",
       " 0.0020248998061615977\n",
       " 0.003351909209025156\n",
       " 0.0\n",
       " 0.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "dstress"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Convert to GPa:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "C11: 156.51814745572818 GPa\n",
      "C12: 59.57460898118596 GPa\n",
      "C44: 98.61657345241198 GPa\n",
      " GPa\n",
      "C12: 59.57460898118596 GPa\n",
      "C44: 98.61657345241198 GPa\n"
     ]
    }
   ],
   "source": [
    "println(\"C11: \", uconvert(u\"GPa\", dstress[1] * u\"hartree\" / u\"bohr\"^3))\n",
    "println(\"C12: \", uconvert(u\"GPa\", dstress[2] * u\"hartree\" / u\"bohr\"^3))\n",
    "println(\"C44: \", uconvert(u\"GPa\", dstress[4] * u\"hartree\" / u\"bohr\"^3))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "These results can be compared directly to finite differences of the stress_fn:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -8.453543098614                   -0.94    5.4    656ms\n",
      "  2   -8.455636235179       -2.68       -1.77    1.0    333ms\n",
      "  3   -8.455781729855       -3.84       -2.89    1.8    435ms\n",
      "  4   -8.455795193357       -4.87       -3.26    3.1    986ms\n",
      "  5   -8.455795301917       -6.96       -3.78    1.1    332ms\n",
      "  6   -8.455795314332       -7.91       -4.82    1.4    410ms\n",
      "  7   -8.455795315191       -9.07       -5.69    2.8    616ms\n",
      "  8   -8.455795315198      -11.15       -6.41    2.0    376ms\n",
      "n     Energy            log10(ΔE)   log10(Δρ)   Diag   Δtime\n",
      "---   ---------------   ---------   ---------   ----   ------\n",
      "  1   -8.453493228720                   -0.94    5.4    388ms\n",
      "  2   -8.455624490153       -2.67       -1.77    1.0    163ms\n",
      "  3   -8.455770064867       -3.84       -2.89    1.9    219ms\n",
      "  4   -8.455782148007       -4.92       -3.31    3.1    300ms\n",
      "  5   -8.455782242303       -7.03       -3.83    1.1    167ms\n",
      "  6   -8.455782253566       -7.95       -4.87    1.5    685ms\n",
      "  7   -8.455782254364       -9.10       -5.52    2.8    244ms\n",
      "  8   -8.455782254371      -11.14       -6.11    1.7    199ms\n",
      "C11 (FD): 156.3324342330684 GPa\n",
      "C12 (FD): 59.561962669811926 GPa\n",
      "C44 (FD): 98.53506719932705 GPa\n"
     ]
    }
   ],
   "source": [
    "h = 1e-3\n",
    "dstress_fd = (stress_fn(h * strain_pattern) - stress_fn(-h * strain_pattern)) / 2h\n",
    "println(\"C11 (FD): \", uconvert(u\"GPa\", dstress_fd[1] * u\"hartree\" / u\"bohr\"^3))\n",
    "println(\"C12 (FD): \", uconvert(u\"GPa\", dstress_fd[2] * u\"hartree\" / u\"bohr\"^3))\n",
    "println(\"C44 (FD): \", uconvert(u\"GPa\", dstress_fd[4] * u\"hartree\" / u\"bohr\"^3))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Here are AD-DFPT results from increasing discretization parameters:\n",
    "\n",
    "| Ecut | kgrid         | c11 [GPa]   | c12 [GPa]  | c44 [GPa]  |\n",
    "|------|---------------|-------:|------:|-------:|\n",
    "| 18   | [4, 4, 4]     | 156.51 | 59.57 |  98.61 |\n",
    "| 18   | [8, 8, 8]     | 153.53 | 56.90 | 100.07 |\n",
    "| 24   | [8, 8, 8]     | 153.26 | 56.82 |  99.97 |\n",
    "| 24   | [14, 14, 14]  | 153.03 | 56.71 | 100.09 |\n",
    "\n",
    "For comparison, Materials Project for PBE *relaxed-ion* elastic constants of\n",
    "silicon [mp-149](https://next-gen.materialsproject.org/materials/mp-149):\n",
    "c11 = 153 GPa, c12 = 57 GPa, c44 = 74 GPa.\n",
    "Note the discrepancy in c44, which is due to us not yet including\n",
    "ionic relaxation in this example."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.1",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 3
}
